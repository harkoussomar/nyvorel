pragma Singleton

import qs.modules.common
import qs.modules.common.functions
import QtQuick
import Quickshell
import Quickshell.Io

Singleton {
    id: root

    property string killDialogQmlPath: FileUtils.trimFileProtocol(Quickshell.shellPath("killDialog.qml"))

    function load() {
        // dummy to force init
    }

    Connections {
        target: Config
        function onReadyChanged() {
            if (Config.ready) checkConflictsProc.running = true
        }
    }

    Process {
        id: checkConflictsProc
        command: ["bash", "-c", `
            watcher_pid="$(
                busctl --user --no-pager list 2>/dev/null |
                awk '$1 == "org.kde.StatusNotifierWatcher" { print $2; exit }'
            )"

            tray_conflict=""

            if [ -n "$watcher_pid" ]; then
                watcher_process="$(
                    cat "/proc/$watcher_pid/comm" 2>/dev/null
                )"

                if [ "$watcher_process" = "kded6" ]; then
                    tray_conflict="$watcher_pid"
                fi
            fi

            echo "$tray_conflict;$(pidof mako dunst 2>/dev/null)"
        `]
        stdout: StdioCollector {
            onStreamFinished: {
                const output = this.text;
                const conflictingTrays = output.split(";")[0].trim().length > 0;
                const conflictingNotifications = output.split(";")[1].trim().length > 0;
                var openDialog = false;
                if (conflictingTrays) {
                    if (!Config.options.conflictKiller.autoKillTrays) openDialog = true;
                    else Quickshell.execDetached(["killall", "kded6"])
                }
                if (conflictingNotifications) {
                    if (!Config.options.conflictKiller.autoKillNotificationDaemons) openDialog = true;
                    else Quickshell.execDetached(["killall", "mako", "dunst"])
                }
                if (openDialog) {
                    Quickshell.execDetached(["qs", "-p", root.killDialogQmlPath])
                }
            }
        }
    }
}
